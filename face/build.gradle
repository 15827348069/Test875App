//控制组件模式和集成模式
if (rootProject.ext.isFaceApplication) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}

android {
    compileSdkVersion rootProject.ext.android["compileSdkVersion"]
    buildToolsVersion rootProject.ext.android["buildToolsVersion"]

    defaultConfig {
        minSdkVersion rootProject.ext.android["minSdkVersion"]
        targetSdkVersion rootProject.ext.android["targetSdkVersion"]
        versionCode rootProject.ext.android["versionCode"]
        versionName rootProject.ext.android["versionName"]
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        if (rootProject.ext.isFaceApplication) {
            applicationId "com.face"
        }
        //如果是集成模式下，则设置ARouter的配置，用于组件间通信的实现
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    //jdk1.8
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    //设置资源前缀
    resourcePrefix "face_"
    sourceSets {
        main {
            if (rootProject.ext.isFaceApplication) {
                manifest.srcFile 'src/main/module/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
            }
            jniLibs.srcDirs = ['libs']
        }
    }

    lintOptions {
        abortOnError false
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar','*.aar'])
    implementation project(':baselibrary')
    implementation project(':idcard')
    annotationProcessor rootProject.ext.dependencies["router-compiler"]
   /* implementation(name: 'zqznface-tmp.190918', ext: 'aar')
    implementation(name: 'zqznfacesdk-tmp.190918', ext: 'aar')*/
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'org.ligboy.retrofit2:converter-fastjson-android:2.1.0'

   // bugly
    implementation 'com.tencent.bugly:crashreport:2.6.6.1'
    implementation 'com.tencent.bugly:nativecrashreport:3.3.1'
}

